package MixedDomain "Heat transfer components mixed with other domains" 
  extends Modelica.Icons.Library;
  
  model HeaterElement "Heating element" 
    extends Modelica.Electrical.Analog.Basic.Resistor;
    parameter Real efficiency=.9 "Efficiency of transformation";
    Interfaces.Node_a thermal annotation (extent=[-10, 40; 10, 60]);
    annotation (
      Diagram, 
      Coordsys(
        extent=[-100, -100; 100, 100], 
        grid=[2, 2], 
        component=[20, 20]), 
      Window(
        x=0.34, 
        y=0.34, 
        width=0.6, 
        height=0.6), 
      Icon(
        Line(points=[-60, 0; -50, 20; -40, -20; -30, 20; -20, -20], style(
              color=45)), 
        Line(points=[-20, -20; -10, 20; 0, -20; 10, 20; 20, -20], style(color=
                41)), 
        Line(points=[20, -20; 30, 20; 40, -20; 50, 20; 60, 0], style(color=45)
          ), 
        Line(points=[-60, 0; -90, 0], style(color=0)), 
        Line(points=[60, 0; 90, 0], style(color=0)), 
        Line(points=[0, 50; 0, -18], style(color=41))), 
      Documentation(info="This heating element model allows energy in the electrical domain to manifest itself as thermal
energy.  The basic principle of the model is compute the power generated by the element (basic
on the resistance of the element) and to transform that power (subject to an efficiency factor)
into thermal energy.
"));
  equation 
    thermal.q = -i^2*R*efficiency;
  end HeaterElement;
  model Thermocouple "Experimental thermocouple" 
    extends Modelica.Electrical.Analog.Interfaces.OnePort;
    parameter Real C[:] "Calibration coefficients";
    Interfaces.Node_a node_a annotation (extent=[-10, -110; 10, -90]);
    annotation (
      Diagram, 
      Icon(
        Polygon(points=[-90, 4; -90, -4; -8, -4; -8, -90; 8, -90; 8, -4; 90, -
              4; 90, 4; -90, 4], style(pattern=0, fillColor=41)), 
        Line(points=[-4, 0; -4, -90], style(color=0)), 
        Line(points=[-90, 0; -4, 0], style(color=0)), 
        Line(points=[4, 0; 90, 0], style(color=0)), 
        Line(points=[4, 0; 4, -90], style(color=0)), 
        Text(extent=[-100, 40; 100, 100], string="%name")), 
      Coordsys(
        extent=[-100, -100; 100, 100], 
        grid=[2, 2], 
        component=[20, 20]), 
      Window(
        x=0.12, 
        y=0.05, 
        width=0.6, 
        height=0.6), 
      Documentation(info="This thermocouple is a model of a real-world experimental thermocouple.  It assumes that
the relationship between the voltage drop across the thermocouple (in the absence of any
electrical current flow) and the temperature of the thermocouple is governed by the
polynomial relationship:

V = sum(c_i*T^i)

The 'c' array is specified as a parameter of the thermocouple.  Because this emperical model
for thermocouple behavior is widely used, it should be possible to determine the values
for the 'c' array based on the published performance characteristics of thermocouples.
"));
    function polyval 
      input Real c[:];
      input Real u;
      output Real y;
    protected 
      Integer n;
    algorithm 
      n := size(c, 1);
      y := 0;
      for i in 1:n loop
        y := y + c[i]*u^(n - i);
      end for;
    end polyval;
  equation 
    v = polyval(C, node_a.T);
    node_a.q = 0;
  end Thermocouple;
  model RotationalSpring "Temperature sensitive rotational spring" 
    extends Modelica.Mechanics.Rotational.Interfaces.Compliant;
    parameter Real c(
      final unit="N.m/rad", 
      final min=0) = 1 "Spring constant";
    parameter Modelica.SIunits.Temperature T_nom=273.15 "Nominal temperature";
    parameter Real dudT(final unit="rad/L") = 1.0 
      "Angular expansion coefficient";
    parameter Modelica.SIunits.Angle unstretched_nom=0.0 
      "Nominal unstretched length (at T_nom)";
    Modelica.SIunits.Angle phi_rel0(start=0) "Unstretched spring angle";
    Modelica.SIunits.Angle dphi "Delta phi";
    Modelica.SIunits.Energy P;
    annotation (
      Icon(
        Line(points=[-100, 0; -60, 0; -44, -30; -16, 30; 17, -30; 47, 30; 62, 
              0; 100, 0], style(
            color=0, 
            pattern=1, 
            thickness=1, 
            arrow=0)), 
        Line(points=[0, 0; 0, 50], style(color=41)), 
        Text(extent=[-100, -100; 100, -40], string="%name")), 
      Coordsys(
        extent=[-100, -100; 100, 100], 
        grid=[2, 2], 
        component=[20, 20]), 
      Window(
        x=0.3, 
        y=0.23, 
        width=0.6, 
        height=0.48), 
      Documentation(info="This rotational spring model takes into account the effect on the springs unstretched length due to
thermal expansion.  The spring is essentially a linear rotational spring just like the one provided
in the Modelica.Mechanics.Rotational library except that the unstretched length of the spring is
subject to thermal expansion.  The sensitivity of the unstretched length to temperature is given
by the 'dudT' parameter.
"));
    Interfaces.Node_a node_a annotation (extent=[-10, 50; 10, 70]);
  equation 
    dphi = (phi_rel - phi_rel0);
    P = c*dphi^2;
    der(P) = node_a.q;
    phi_rel0 = unstretched_nom + dudT*(node_a.T - T_nom);
    tau = c*dphi;
  end RotationalSpring;
  annotation (Coordsys(
      extent=[0, 0; 944, 593], 
      grid=[1, 1], 
      component=[20, 20]), Window(
      x=0.39, 
      y=0.39, 
      width=0.6, 
      height=0.6, 
      library=1, 
      autolayout=1));
end MixedDomain;
